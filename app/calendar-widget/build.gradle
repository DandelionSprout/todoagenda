buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
        classpath 'org.ajoberstar:grgit:2.1.0'
    }
}

apply plugin: 'com.android.application'

repositories {
    google()
    jcenter()
}

dependencies {
    implementation 'joda-time:joda-time:2.9.7'
    implementation 'com.larswerkman:HoloColorPicker:1.5'
    implementation 'com.android.support:appcompat-v7:27.1.1'
}

android {

    compileSdkVersion 27

    defaultConfig {
        versionName '1.10.1' + "-" + getCommitShortHash()
        versionCode getCommitCount()
        minSdkVersion 15
        targetSdkVersion 27

        testApplicationId "org.andstatus.todoagenda.tests"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            lintOptions {
                warning 'MissingTranslation','InvalidPackage'
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        abortOnError false
    }

    if (project.hasProperty('todoagendaStoreFile')) {
        signingConfigs {
            releaseConfig {
                storeFile file(todoagendaStoreFile)
                storePassword todoagendaStorePassword
                keyAlias todoagendaKeyAlias
                keyPassword todoagendaKeyPassword
            }
        }
        buildTypes.release.signingConfig signingConfigs.releaseConfig
    }
    // See https://www.timroes.de/2013/09/22/handling-signing-configs-with-gradle/
    if (project.hasProperty("todoagenda.signing")
            && new File(project.property("todoagenda.signing").toString() + ".gradle").exists()) {
        apply from: project.property("todoagenda.signing") + ".gradle";
    }
}

static getCommitShortHash() {
    try {
        def git = org.ajoberstar.grgit.Grgit.open()
        def versionNameSuffix = "${git.head().getAbbreviatedId(7)}"
        git.close()
        return versionNameSuffix
    } catch (ignored) {
        return ""
    }
}

static getCommitCount() {
    try {
        def git = org.ajoberstar.grgit.Grgit.open()
        def versionCode = git.log().size()
        git.close()
        return versionCode
    } catch (ignored) {
        return 1
    }
}
